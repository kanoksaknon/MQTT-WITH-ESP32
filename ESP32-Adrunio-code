#include <WiFi.h>
#include <PubSubClient.h>
#include <DHTesp.h>

const int DHT_PIN = 15;
const int RELAY_PIN = 26; // Change to match your setup

DHTesp dht;
const char* ssid = "Wokwi-GUEST"; // WiFi SSID
const char* password = "";
const char* mqtt_server = "broker.emqx.io"; // MQTT Broker URL

WiFiClient espClient;
PubSubClient client(espClient);
unsigned long lastMsg = 0;

void setup_wifi() {
  delay(10);
  Serial.println();
  Serial.print("Connecting to ");
  Serial.println(ssid);

  WiFi.mode(WIFI_STA);
  WiFi.begin(ssid, password);

  while (WiFi.status() != WL_CONNECTED) {
    delay(500);
    Serial.print(".");
  }

  randomSeed(micros());

  Serial.println("");
  Serial.println("WiFi connected");
  Serial.println("IP address: ");
  Serial.println(WiFi.localIP());
}

void callback(char* topic, byte* payload, unsigned int length) {
  Serial.print("Message arrived [");
  Serial.print(topic);
  Serial.print("] ");
  String message = "";
  for (int i = 0; i < length; i++) {
    message += (char)payload[i];
  }

  if (String(topic) == "/msg/relay") {
    Serial.print("Received message: "); // Print received message
    Serial.println(message);

    if (message == "ON") {
      digitalWrite(RELAY_PIN, HIGH); // Turn on relay
    } else if (message == "OFF") {
      digitalWrite(RELAY_PIN, LOW); // Turn off relay
    }
  }
}

void reconnect() {
  while (!client.connected()) {
    Serial.print("Attempting MQTT connection...");
    String clientId = "ESP32Client-";
    clientId += String(random(0xffff), HEX);
    if (client.connect(clientId.c_str())) {
      Serial.println("Connected");
      client.subscribe("/msg/relay");
    } else {
      Serial.print("failed, rc=");
      Serial.print(client.state());
      Serial.println(" try again in 5 seconds");
      delay(5000);
    }
  }
}

void setup() {
  pinMode(RELAY_PIN, OUTPUT); // Set relay pin as output
  Serial.begin(115200);
  setup_wifi();
  client.setServer(mqtt_server, 1883);
  client.setCallback(callback);
  dht.setup(DHT_PIN, DHTesp::DHT22); // Initialize DHT sensor
}

void loop() {
  if (!client.connected()) {
    reconnect();
  }
  client.loop();

  unsigned long now = millis();
  if (now - lastMsg > 2000) { // Publish data command
    lastMsg = now;
    // Code for reading DHT sensor data and publishing to MQTT topic
    TempAndHumidity data = dht.getTempAndHumidity();

    String temp = String(data.temperature, 2);
    String hum = String(data.humidity, 1);

    // Publish temperature and humidity to separate topics
    client.publish("DHTsensor/temp", temp.c_str()); // publish temp topic
    client.publish("DHTsensor/humid", hum.c_str()); // publish humidity topic

    delay(3000);
    Serial.print("Temperature: ");
    Serial.println(temp);
    Serial.print("Humidity: ");
    Serial.println(hum);
  }
}
